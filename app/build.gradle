apply plugin: 'com.android.application'
ext.densityCodes = ['mdpi': 1, 'hdpi': 2, 'xhdpi': 3, 'xxhdpi': 4, 'xxxhdpi': 5]

import com.android.build.OutputFile

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "keyboard.kirjasoft.org.kirjakeyboard"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 8009
        versionName "0.8.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resConfigs "en", "hu"
    }
    splits {
        density {
            enable true
            reset()
            include "mdpi", "hdpi", "xhdpi", "xxhdpi", "xxxhdpi"
            compatibleScreens 'small', 'normal', 'large', 'xlarge'
        }
    }
    lintOptions {

        checkReleaseBuilds false

    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    android.applicationVariants.all { variant ->

        variant.outputs.each { output ->

            def baseDensityVersionCode = project.ext.densityCodes.get(output.getFilter(OutputFile.DENSITY))

            if (baseDensityVersionCode != null) {
                output.versionCodeOverride =
                        baseDensityVersionCode * 10000000 + variant.versionCode
            }
        }
    }
}

dependencies {
    implementation 'com.android.support:design:27.0.2'
    api fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    api 'com.android.support:appcompat-v7:27.0.2'
    api 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
}
